buildscript {
    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.17'
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
        classpath 'org.openjfx:javafx-plugin:0.0.11-SNAPSHOT'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.5.6'
    }
}

configure(rootProject) {

    // remove the 'bisq-*' scripts and 'lib' dir generated by the 'installDist' task
    task clean {
        doLast {
            delete fileTree(dir: rootProject.projectDir, include: 'bisq-*'), 'lib'
        }
    }
}

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'com.google.osdetector'

    sourceCompatibility = 1.11

    ext { // in alphabetical order
        bcVersion = '1.63'
        bitcoinjVersion = '42bbae9'
        codecVersion = '1.13'
        cowwocVersion = '1.2'
        easybindVersion = '1.0.3'
        easyVersion = '4.0.1'
        findbugsVersion = '3.0.2'
        firebaseVersion = '6.2.0'
        fontawesomefxVersion = '8.0.0'
        fontawesomefxCommonsVersion = '9.1.2'
        fontawesomefxMaterialdesignfontVersion = '2.0.26-9.1.2'
        grpcVersion = '1.42.1'
        gsonVersion = '2.8.5'
        guavaVersion = '28.2-jre'
        guiceVersion = '5.0.1'
        hamcrestVersion = '1.3'
        httpclientVersion = '4.5.12'
        httpcoreVersion = '4.4.13'
        ioVersion = '2.6'
        jacksonVersion = '2.12.1'
        javafxVersion = '17.0.1'
        javaxAnnotationVersion = '1.2'
        jcsvVersion = '1.4.0'
        jetbrainsAnnotationsVersion = '13.0'
        jfoenixVersion = '9.0.10'
        joptVersion = '5.0.4'
        jsonsimpleVersion = '1.1.1'
        jsonrpc4jVersion = '1.6.0.bisq.1'
        junitVersion = '4.12'
        jupiterVersion = '5.7.0'
        kotlinVersion = '1.3.41'
        knowmXchangeVersion = '4.4.2'
        langVersion = '3.11'
        logbackVersion = '1.1.11'
        loggingVersion = '1.2'
        lombokVersion = '1.18.22'
        mockitoVersion = '4.0.0'
        netlayerVersion = '0.7.2' // Tag from https://github.com/bisq-network/netlayer/tags based on externaltor branch
        protobufVersion = '3.19.1'
        protocVersion = protobufVersion
        pushyVersion = '0.13.2'
        qrgenVersion = '1.3'
        slf4jVersion = '1.7.30'
        sparkVersion = '2.5.2'
        springBootVersion = '2.5.6'

        os = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        testImplementation "junit:junit:$junitVersion"
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}


configure([project(':cli'),
           project(':daemon'),
           project(':desktop'),
           project(':monitor'),
           project(':relay'),
           project(':seednode'),
           project(':statsnode'),
           project(':pricenode'),
           project(':inventory'),
           project(':apitest')]) {

    apply plugin: 'application'

    build.dependsOn installDist
    installDist.destinationDir = file('build/app')
    distZip.enabled = false

    // the 'installDist' and 'startScripts' blocks below configure bisq executables to put
    // generated shell scripts in the root project directory, such that users can easily
    // discover and invoke e.g. ./bisq-desktop, ./bisq-seednode, etc.
    // See https://stackoverflow.com/q/46327736 for details.

    installDist {
        doLast {
            // copy generated shell scripts, e.g. `bisq-desktop` directly to the project
            // root directory for discoverability and ease of use

            copy {
                from "$destinationDir/bin"
                into rootProject.projectDir
            }
            // copy libs required for generated shell script classpaths to 'lib' dir under
            // the project root directory
            copy {
                from "$destinationDir/lib"
                into "${rootProject.projectDir}/lib"
            }

            // edit generated shell scripts such that they expect to be executed in the
            // project root dir as opposed to a 'bin' subdirectory
            def windowsScriptFile = file("${rootProject.projectDir}/bisq-${applicationName}.bat")
            windowsScriptFile.text = windowsScriptFile.text.replace(
                'set APP_HOME=%DIRNAME%..', 'set APP_HOME=%DIRNAME%')

            def unixScriptFile = file("${rootProject.projectDir}/bisq-$applicationName")
            unixScriptFile.text = unixScriptFile.text.replace(
                'APP_HOME=$( cd "${APP_HOME:-./}.." && pwd -P ) || exit', 'APP_HOME=$( cd "${APP_HOME:-./}" && pwd -P ) || exit')

            if (applicationName == 'desktop') {
                def script = file("${rootProject.projectDir}/bisq-$applicationName")
                script.text = script.text.replace(
                    'DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="-XX:MaxRAM=8g -Xss1280k -XX:+UseG1GC ' +
                    '-XX:MaxHeapFreeRatio=10 -XX:MinHeapFreeRatio=5 -XX:+UseStringDeduplication ' +
                    '-Djava.net.preferIPv4Stack=true"')
            }

            if (applicationName == 'apitest') {
                // Pass the logback config file as a system property to avoid chatty
                // logback startup due to multiple logback.xml files in the classpath
                // (:daemon & :cli).
                def script = file("${rootProject.projectDir}/bisq-$applicationName")
                script.text = script.text.replace(
                    'DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="' +
                    '-Dlogback.configurationFile=apitest/build/resources/main/logback.xml"')
            }

            if (osdetector.os != 'windows')
                delete fileTree(dir: rootProject.projectDir, include: 'bisq-*.bat')
            else
                delete fileTree(dir: rootProject.projectDir, include: 'bisq-*', exclude: '*.bat')
        }
    }

    startScripts {
        // rename scripts from, e.g. `desktop` to `bisq-desktop`
        applicationName = "bisq-$applicationName"
    }
}

configure(project(':proto')) {
    apply plugin: 'com.google.protobuf'

    dependencies {
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "ch.qos.logback:logback-core:$logbackVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "com.google.protobuf:protobuf-java:$protobufVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation("io.grpc:grpc-protobuf:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
        implementation("io.grpc:grpc-stub:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
    }

    sourceSets.main.java.srcDirs += [
        'build/generated/source/proto/main/grpc',
        'build/generated/source/proto/main/java'
    ]

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:${protocVersion}"
        }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
            }
        }
        generateProtoTasks {
            all()*.plugins { grpc {} }
        }
    }
}

configure(project(':assets')) {
    dependencies {
        implementation("com.github.bisq-network:bitcoinj:$bitcoinjVersion") {
            exclude(module: 'bcprov-jdk15on')
            exclude(module: 'guava')
            exclude(module: 'jsr305')
            exclude(module: 'okhttp')
            exclude(module: 'okio')
            exclude(module: 'protobuf-java')
            exclude(module: 'slf4j-api')
        }
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "org.apache.commons:commons-lang3:$langVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
    }
}


configure(project(':common')) {
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        version = "$javafxVersion"
        modules = ['javafx.graphics']
    }

    dependencies {
        implementation project(':proto')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "ch.qos.logback:logback-core:$logbackVersion"
        implementation("com.github.bisq-network:bitcoinj:$bitcoinjVersion") {
            exclude(module: 'bcprov-jdk15on')
            exclude(module: 'guava')
            exclude(module: 'jsr305')
            exclude(module: 'okhttp')
            exclude(module: 'okio')
            exclude(module: 'protobuf-java')
            exclude(module: 'slf4j-api')
        }
        implementation "com.google.code.findbugs:jsr305:$findbugsVersion"
        implementation "com.google.code.gson:gson:$gsonVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation("com.google.inject:guice:$guiceVersion") {
            exclude(module: 'guava')
        }
        implementation "com.google.protobuf:protobuf-java:$protobufVersion"
        implementation "commons-io:commons-io:$ioVersion"
        implementation "net.sf.jopt-simple:jopt-simple:$joptVersion"
        implementation "org.apache.commons:commons-lang3:$langVersion"
        implementation "org.bouncycastle:bcpg-jdk15on:$bcVersion"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        implementation "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
        runtimeOnly("io.grpc:grpc-netty-shaded:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
    }
}


configure(project(':p2p')) {
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        version = "$javafxVersion"
        modules = ['javafx.base']
    }

    dependencies {
        implementation project(':proto')
        implementation project(':common')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "com.google.protobuf:protobuf-java:$protobufVersion"
        implementation "org.fxmisc.easybind:easybind:$easybindVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation("com.github.bisq-network.netlayer:tor.external:$netlayerVersion") {
            exclude(module: 'slf4j-api')
        }
        implementation("com.github.bisq-network.netlayer:tor.native:$netlayerVersion") {
            exclude(module: 'slf4j-api')
        }
        implementation("com.github.bisq-network:bitcoinj:$bitcoinjVersion") {
            exclude(module: 'bcprov-jdk15on')
            exclude(module: 'guava')
            exclude(module: 'jsr305')
            exclude(module: 'okhttp')
            exclude(module: 'okio')
            exclude(module: 'protobuf-java')
            exclude(module: 'slf4j-api')
        }
        implementation("com.google.inject:guice:$guiceVersion") {
            exclude(module: 'guava')
        }
        implementation("org.apache.httpcomponents:httpclient:$httpclientVersion") {
            exclude(module: 'commons-codec')
        }
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
        testImplementation "ch.qos.logback:logback-core:$logbackVersion"
        testImplementation "org.apache.commons:commons-lang3:$langVersion"
        testImplementation("org.mockito:mockito-core:$mockitoVersion")
    }

    processResources.doFirst {
        // Sanity check that Git LFS-managed data store files have actually been sync'd.
        // If they have not, e.g. because Git LFS is not installed, they will be text files
        // containing a sha256 hash of the remote object, indicating we should stop the
        // build and inform the user how to fix the problem.
        if (file('src/main/resources/ProposalStore_BTC_MAINNET').text.contains("oid sha256:"))
            throw new GradleException("p2p data store files have not been synchronized. " +
                "To fix this, ensure you have Git LFS installed and run `git lfs pull`. " +
                "See docs/build.md for more information.")
    }
}


configure(project(':core')) {
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        version = "$javafxVersion"
        modules = ['javafx.base']
    }

    dependencies {
        implementation project(':proto')
        implementation project(':assets')
        implementation project(':common')
        implementation project(':p2p')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "ch.qos.logback:logback-core:$logbackVersion"
        implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
        implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
        implementation "com.google.code.findbugs:jsr305:$findbugsVersion"
        implementation "com.google.code.gson:gson:$gsonVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "com.google.protobuf:protobuf-java:$protobufVersion"
        implementation "commons-codec:commons-codec:$codecVersion"
        implementation "commons-io:commons-io:$ioVersion"
        implementation "net.sf.jopt-simple:jopt-simple:$joptVersion"
        implementation "org.apache.commons:commons-lang3:$langVersion"
        implementation "org.apache.httpcomponents:httpcore:$httpcoreVersion"
        implementation "org.fxmisc.easybind:easybind:$easybindVersion"
        implementation "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion") {
            exclude(module: 'jackson-annotations')
        }
        implementation("com.github.bisq-network.netlayer:tor.external:$netlayerVersion") {
            exclude(module: 'slf4j-api')
        }
        implementation("com.github.bisq-network.netlayer:tor.native:$netlayerVersion") {
            exclude(module: 'slf4j-api')
        }
        implementation("com.github.bisq-network:bitcoinj:$bitcoinjVersion") {
            exclude(module: 'bcprov-jdk15on')
            exclude(module: 'guava')
            exclude(module: 'jsr305')
            exclude(module: 'okhttp')
            exclude(module: 'okio')
            exclude(module: 'protobuf-java')
            exclude(module: 'slf4j-api')
        }
        implementation("com.github.bisq-network:jsonrpc4j:$jsonrpc4jVersion") {
            exclude(module: 'base64')
            exclude(module: 'httpcore-nio')
        }
        implementation("com.google.inject:guice:$guiceVersion") {
            exclude(module: 'guava')
        }
        implementation("org.apache.httpcomponents:httpclient:$httpclientVersion") {
            exclude(module: 'commons-codec')
        }
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testImplementation "com.natpryce:make-it-easy:$easyVersion"
        testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
    }

    test {
        systemProperty 'jdk.attach.allowAttachSelf', true
    }
}

configure(project(':cli')) {
    mainClassName = 'bisq.cli.CliMain'

    dependencies {
        implementation project(':proto')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "ch.qos.logback:logback-core:$logbackVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "com.google.protobuf:protobuf-java:$protobufVersion"
        implementation "net.sf.jopt-simple:jopt-simple:$joptVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation("io.grpc:grpc-core:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
        implementation("io.grpc:grpc-stub:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
        runtimeOnly("io.grpc:grpc-netty-shaded:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testImplementation "org.bitbucket.cowwoc:diff-match-patch:$cowwocVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$jupiterVersion"
        testRuntimeOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$jupiterVersion")
    }

    test {
        useJUnitPlatform()
    }
}

configure(project(':desktop')) {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'org.openjfx.javafxplugin'
    apply from: 'package/package.gradle'

    javafx {
        version = "$javafxVersion"
        modules = ['javafx.controls', 'javafx.fxml']
    }

    version = '1.7.5-SNAPSHOT'

    jar.manifest.attributes(
        "Implementation-Title": project.name,
        "Implementation-Version": version)

    mainClassName = 'bisq.desktop.app.BisqAppMain'

    jar {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    sourceSets.main.resources.srcDirs += ['src/main/java'] // to copy fxml and css files

    dependencies {
        implementation project(':assets')
        implementation project(':common')
        implementation project(':proto')
        implementation project(':p2p')
        implementation project(':core')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "ch.qos.logback:logback-core:$logbackVersion"
        implementation "com.google.code.gson:gson:$gsonVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "com.google.protobuf:protobuf-java:$protobufVersion"
        implementation "com.googlecode.jcsv:jcsv:$jcsvVersion"
        //implementation "com.jfoenix:jfoenix:$jfoenixVersion"               // until sshahine/JFoenix#1187
        implementation("org.rationalityfrontline.workaround:jfoenix:17.0.1") // is fixed
        implementation "commons-io:commons-io:$ioVersion"
        implementation "de.jensd:fontawesomefx-commons:$fontawesomefxCommonsVersion"
        implementation "de.jensd:fontawesomefx-materialdesignfont:$fontawesomefxMaterialdesignfontVersion"
        implementation "de.jensd:fontawesomefx:$fontawesomefxVersion"
        implementation "net.glxn:qrgen:$qrgenVersion"
        implementation "org.apache.commons:commons-lang3:$langVersion"
        implementation "org.bouncycastle:bcpg-jdk15on:$bcVersion"
        implementation "org.fxmisc.easybind:easybind:$easybindVersion"
        implementation "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation("com.github.bisq-network:bitcoinj:$bitcoinjVersion") {
            exclude(module: 'bcprov-jdk15on')
            exclude(module: 'guava')
            exclude(module: 'jsr305')
            exclude(module: 'okhttp')
            exclude(module: 'okio')
            exclude(module: 'protobuf-java')
            exclude(module: 'slf4j-api')
        }
        implementation("com.google.inject:guice:$guiceVersion") {
            exclude(module: 'guava')
        }
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testImplementation "com.natpryce:make-it-easy:$easyVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
    }

    test {
        systemProperty 'jdk.attach.allowAttachSelf', true
    }
}


configure(project(':monitor')) {
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        version = "$javafxVersion"
        modules = ['javafx.base']
    }

    mainClassName = 'bisq.monitor.Monitor'

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dependencies {
        implementation project(':assets')
        implementation project(':common')
        implementation project(':core')
        implementation project(':p2p')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "ch.qos.logback:logback-core:$logbackVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation("com.github.bisq-network.netlayer:tor.external:$netlayerVersion") {
            exclude(module: 'slf4j-api')
        }
        implementation("com.github.bisq-network.netlayer:tor.native:$netlayerVersion") {
            exclude(module: 'slf4j-api')
        }
        implementation("com.google.inject:guice:$guiceVersion") {
            exclude(module: 'guava')
        }
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$jupiterVersion"
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$jupiterVersion")
    }
}


configure(project(':pricenode')) {
    apply plugin: "org.springframework.boot"

    mainClassName = 'bisq.price.Main'

    version = file("src/main/resources/version.txt").text.trim()

    jar.manifest.attributes(
        "Implementation-Title": project.name,
        "Implementation-Version": version)

    dependencies {
        implementation project(":common")
        implementation project(":core")
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "com.google.code.gson:gson:$gsonVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "commons-codec:commons-codec:$codecVersion"
        implementation "org.apache.httpcomponents:httpcore:$httpcoreVersion"
        implementation("org.apache.httpcomponents:httpclient:$httpclientVersion") {
            exclude(module: 'commons-codec')
        }
        implementation("org.knowm.xchange:xchange-binance:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-bitbay:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-bitfinex:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-bitflyer:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-bitstamp:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-btcmarkets:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-cexio:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-coinmarketcap:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-coinmate:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-coinone:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-exmo:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-hitbtc:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-huobi:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-independentreserve:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-kraken:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-luno:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-mercadobitcoin:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-paribu:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-poloniex:$knowmXchangeVersion")
        implementation("org.knowm.xchange:xchange-quoine:$knowmXchangeVersion")
        implementation("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$jupiterVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$jupiterVersion")
    }

    test {
        useJUnitPlatform()

        // Disabled by default, since spot provider tests include connections to external API endpoints
        // Can be enabled by adding -Dtest.pricenode.includeSpotProviderTests=true to the gradle command:
        // ./gradlew test -Dtest.pricenode.includeSpotProviderTests=true
        if (System.properties['test.pricenode.includeSpotProviderTests'] != 'true') {
            project.logger.lifecycle('Pricenode: Skipping spot provider tests')
            exclude 'bisq/price/spot/providers/**'
        }
    }

    task stage {
        dependsOn assemble
    }
}


configure(project(':relay')) {
    mainClassName = 'bisq.relay.RelayMain'

    dependencies {
        implementation project(':common')
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "ch.qos.logback:logback-core:$logbackVersion"
        implementation("com.google.firebase:firebase-admin:$firebaseVersion") {
            exclude(module: 'commons-logging')
            exclude(module: 'grpc-auth')
            exclude(module: 'httpclient')
            exclude(module: 'httpcore')
        }
        implementation "com.sparkjava:spark-core:$sparkVersion"
        implementation "com.turo:pushy:$pushyVersion"
        implementation "commons-codec:commons-codec:$codecVersion"
        implementation "io.grpc:grpc-auth:$grpcVersion"
    }
}


configure(project(':seednode')) {
    apply plugin: 'com.github.johnrengelman.shadow'

    mainClassName = 'bisq.seednode.SeedNodeMain'

    dependencies {
        implementation project(':common')
        implementation project(':p2p')
        implementation project(':core')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation("com.google.inject:guice:$guiceVersion") {
            exclude(module: 'guava')
        }
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
    }
}


configure(project(':statsnode')) {
    mainClassName = 'bisq.statistics.StatisticsMain'

    dependencies {
        implementation project(':common')
        implementation project(':p2p')
        implementation project(':core')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation("com.google.inject:guice:$guiceVersion") {
            exclude(module: 'guava')
        }
    }
}

configure(project(':daemon')) {
    mainClassName = 'bisq.daemon.app.BisqDaemonMain'

    dependencies {
        implementation project(':proto')
        implementation project(':common')
        implementation project(':p2p')
        implementation project(':core')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "ch.qos.logback:logback-core:$logbackVersion"
        implementation "com.google.code.gson:gson:$gsonVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "com.google.protobuf:protobuf-java:$protobufVersion"
        implementation "org.apache.commons:commons-lang3:$langVersion"
        implementation "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation("com.github.bisq-network:bitcoinj:$bitcoinjVersion") {
            exclude(module: 'bcprov-jdk15on')
            exclude(module: 'guava')
            exclude(module: 'jsr305')
            exclude(module: 'okhttp')
            exclude(module: 'okio')
            exclude(module: 'protobuf-java')
            exclude(module: 'slf4j-api')
        }
        implementation("com.google.inject:guice:$guiceVersion") {
            exclude(module: 'guava')
        }
        implementation("io.grpc:grpc-protobuf:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
        implementation("io.grpc:grpc-stub:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
        runtimeOnly("io.grpc:grpc-netty-shaded:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$jupiterVersion"
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$jupiterVersion")
    }
}

configure(project(':inventory')) {
    apply plugin: 'com.github.johnrengelman.shadow'

    mainClassName = 'bisq.inventory.InventoryMonitorMain'

    dependencies {
        implementation project(':common')
        implementation project(':p2p')
        implementation project(':core')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "ch.qos.logback:logback-core:$logbackVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "com.sparkjava:spark-core:$sparkVersion"
        implementation "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
        implementation("com.google.inject:guice:$guiceVersion") {
            exclude(module: 'guava')
        }
    }
}

configure(project(':apitest')) {
    mainClassName = 'bisq.apitest.ApiTestMain'

    // The external dao-setup.gradle file contains tasks to install and clean dao-setup
    // files downloaded from
    //          https://github.com/bisq-network/bisq/raw/master/docs/dao-setup.zip
    // These tasks are not run by the default build, but they can can be run during
    // full or partial builds, or by themselves.
    // To run the regular clean + build + test (non api), and install dao-setup files:
    //      ./gradlew clean build :apitest:installDaoSetup
    // To install or re-install dao-setup file only:
    //      ./gradlew :apitest:installDaoSetup -x test
    // To clean installed dao-setup files:
    //      ./gradlew :apitest:cleanDaoSetup -x test
    apply from: 'dao-setup.gradle'

    // We have to disable the :apitest 'test' task by default because we do not want
    // to interfere with normal builds.  To run JUnit tests in this subproject:
    // Run a normal build and install dao-setup files first, then run:
    //              'gradle  :apitest:test -DrunApiTests=true'
    test.enabled = System.getProperty("runApiTests") == "true"

    sourceSets {
        main {
            resources {
                exclude 'dao-setup'
                exclude 'dao-setup.zip'
            }
        }
    }

    test {
        useJUnitPlatform()
        outputs.upToDateWhen { false }          // Don't use previously cached test outputs.
        testLogging {
            showStackTraces = true              // Show full stack traces in the console.
            exceptionFormat = "full"
            // Show passed & failed tests, and anything printed to stderr by the tests in the console.
            // Do not show skipped tests in the console;  they are shown in the html report.
            events "passed", "failed", "standardError"
        }

        afterSuite { desc, result ->
            if (!desc.parent) {
                println("${result.resultType} " +
                    "[${result.testCount} tests, " +
                    "${result.successfulTestCount} passed, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped]  html report contains skipped test info")

                // Show report link if all tests passed in case you want to see more detail, stdout, skipped, etc.
                if (result.resultType == TestResult.ResultType.SUCCESS) {
                    DirectoryReport htmlReport = getReports().getHtml()
                    String reportUrl = new org.gradle.internal.logging.ConsoleRenderer()
                        .asClickableFileUrl(htmlReport.getEntryPoint())
                    println("REPORT  " + reportUrl)
                }
            }
        }
    }

    dependencies {
        implementation project(':proto')
        implementation project(':common')
        implementation project(':core')
        implementation project(':seednode')
        implementation project(':desktop')
        implementation project(':daemon')
        implementation project(':cli')
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "ch.qos.logback:logback-core:$logbackVersion"
        implementation "com.google.code.gson:gson:$gsonVersion"
        implementation "com.google.guava:guava:$guavaVersion"
        implementation "com.google.protobuf:protobuf-java:$protobufVersion"
        implementation "net.sf.jopt-simple:jopt-simple:$joptVersion"
        implementation "org.apache.commons:commons-lang3:$langVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        implementation("com.github.bisq-network:bitcoinj:$bitcoinjVersion") {
            exclude(module: 'bcprov-jdk15on')
            exclude(module: 'guava')
            exclude(module: 'jsr305')
            exclude(module: 'okhttp')
            exclude(module: 'okio')
            exclude(module: 'protobuf-java')
            exclude(module: 'slf4j-api')
        }
        implementation("io.grpc:grpc-protobuf:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
        implementation("io.grpc:grpc-stub:$grpcVersion") {
            exclude(module: 'animal-sniffer-annotations')
            exclude(module: 'guava')
        }
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$jupiterVersion"
        testRuntimeOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$jupiterVersion")
    }
}
